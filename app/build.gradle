apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 1

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "rw.transax.hahiye"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode versionMajor * 1000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            resValue("string", "application_version", "${defaultConfig.versionName}")
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            resValue("string", "app_version", "${defaultConfig.versionName}")
        }
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
}

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.5.1-1' }
    plugins {
        javalite { artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0" }
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.14.0' // CURRENT_GRPC_VERSION
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite {}
                grpc { // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'com.android.support:appcompat-v7:28.0.0-rc02'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-v4:28.0.0-rc02'
    implementation 'com.android.support:support-vector-drawable:28.0.0-rc02'
    implementation 'com.android.support:design:28.0.0-rc02'
    implementation 'com.android.support:cardview-v7:28.0.0-rc02'
    implementation 'com.android.support:recyclerview-v7:28.0.0-rc02'

    /**
     * adding dependencies that will help to use architecture components
     * and Room db for local storage of data
     *
     */

    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'android.arch.persistence.room:runtime:1.1.1'
    implementation 'android.arch.paging:runtime:1.0.1'
    annotationProcessor 'android.arch.lifecycle:compiler:1.1.1'
    annotationProcessor 'android.arch.persistence.room:compiler:1.1.1'

    /**
     * Adding Retrofit Library to make network call
     * and GSON Converted to parse the returned json data.
     */
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'

    // You need to build grpc-java to obtain these libraries below.
    implementation 'io.grpc:grpc-auth:1.14.0' // CURRENT_GRPC_VERSION
    implementation 'io.grpc:grpc-okhttp:1.14.0' // CURRENT_GRPC_VERSION
    implementation 'io.grpc:grpc-protobuf-lite:1.14.0' // CURRENT_GRPC_VERSION
    implementation 'io.grpc:grpc-stub:1.14.0' // CURRENT_GRPC_VERSION
    implementation 'javax.annotation:javax.annotation-api:1.2'

    // workaround for https://github.com/google/protobuf/issues/1889
    protobuf 'com.google.protobuf:protobuf-java:3.4.0'

    /**
     * Others
     */
    implementation 'com.github.doctoror.particlesdrawable:library:1.0.9'
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    implementation 'com.fede987:status-bar-alert:1.0.2'
    implementation 'com.makeramen:roundedimageview:2.3.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}