apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.protobuf'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 1

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "rw.transax.hahiye"
        minSdkVersion 16
        targetSdkVersion 28
        multiDexEnabled true
        versionCode versionMajor * 1000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            resValue("string", "application_version", "${defaultConfig.versionName}")
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            resValue("string", "app_version", "${defaultConfig.versionName}")
        }
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning', 'HardcodedText', 'InvalidPackage'
        textReport true
        textOutput "stdout"
    }

    dataBinding {
        enabled = true
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
}

/**
 * After saving my .proto file app/src/main/proto folder
 * This script have to be added into gradle in order to
 * generate the Proto classes from the PROTO File
 */

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.5.1-1' }
    plugins {
        javalite { artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0" }
        grpc { artifact = 'io.grpc:protoc-gen-grpc-java:1.14.0' } // CURRENT_GRPC_VERSION
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite {}
                grpc { // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.0.1'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'

    /**
     * adding dependencies that will help to use architecture components
     * and Room db for local storage of data
     *
     */

    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.room:room-runtime:2.1.0-alpha03'
    implementation 'androidx.paging:paging-runtime:2.1.0-rc01'
    annotationProcessor 'androidx.lifecycle:lifecycle-compiler:2.0.0'
    annotationProcessor 'androidx.room:room-compiler:2.1.0-alpha03'

    /**
     * Adding Retrofit Library to make network call
     * and GSON Converted to parse the returned json data.
     */
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'

    /* You need to build grpc-java to obtain these libraries below. */
    implementation 'io.grpc:grpc-auth:1.14.0'
    implementation 'io.grpc:grpc-okhttp:1.14.0'
    implementation 'io.grpc:grpc-protobuf-lite:1.14.0'
    implementation 'io.grpc:grpc-stub:1.14.0'
    implementation 'javax.annotation:javax.annotation-api:1.2'

    /* workaround for https://github.com/google/protobuf/issues/1889 */
    protobuf 'com.google.protobuf:protobuf-java:3.4.0'

    implementation 'com.google.android.gms:play-services-base:16.1.0'
    implementation('com.google.auth:google-auth-library-oauth2-http:0.9.0') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    implementation 'com.google.truth:truth:0.36'

    /* Others */
    implementation 'com.github.doctoror.particlesdrawable:library:1.0.9'
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    implementation 'com.fede987:status-bar-alert:1.0.2'
    implementation 'com.makeramen:roundedimageview:2.3.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'de.hdodenhof:circleimageview:3.0.0'
    implementation 'com.thefinestartist:movingbutton:1.0.0'


    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
repositories {
    mavenCentral()
}
